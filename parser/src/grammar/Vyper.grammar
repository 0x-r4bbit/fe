file_input: module_stmt+ ENDMARKER
module_stmt: import_stmt | type_def | contract_def

########################### import_stmt ##############################

import_stmt: (simple_import | from_import) NEWLINE

simple_import: 'import' (simple_import_name (',' simple_import_name)*)
simple_import_name: dotted_name ['as' NAME]

# Note: the ('.' | '...') is necessary because '...' is tokenized as a single
# OP token (an ellipsis OP)
from_import: from_import_parent_alt | from_import_sub_alt

from_import_parent_alt: 'from' ('.' | '...')+ 'import' from_import_names
from_import_sub_alt: 'from' from_import_sub_path 'import' from_import_names

from_import_sub_path: ('.' | '...')* dotted_name
from_import_names: '*' | '(' from_import_names_list ')' | from_import_names_list
from_import_names_list: from_import_name (',' from_import_name)* [',']
from_import_name: NAME ['as' NAME]

########################### type_def #################################

type_def: 'type' NAME '=' type_desc NEWLINE

########################### contract_def #############################

contract_def:
    'contract' NAME ':' NEWLINE
    INDENT
    contract_stmt+
    DEDENT

contract_stmt: contract_field | event_def | func_def

contract_field: [contract_field_qual] NAME ':' type_desc NEWLINE

event_def:
    'event' NAME ':' NEWLINE
    INDENT
    event_field_def+
    DEDENT
event_field: [event_field_qual] NAME ':' type_desc NEWLINE

func_def:
    [func_qual] 'def' NAME '(' [arg_list] ')' ['->' base_type] ':' NEWLINE
    INDENT
    func_stmt+
    DEDENT
arg_list: arg_def (',' arg_def)* [',']
arg_def: NAME ':' type_desc

type_desc: map_type | base_type
map_type:
    # This is required because of the ambigious right-shift token
    'map' '<' base_type ',' 'map' '<' base_type ',' type_desc '>>' |
    'map' '<' base_type ',' type_desc '>'
base_type: NAME arr_list
arr_list: ('[' const_expr ']')*

contract_field_qual: 'const' | 'pub'
event_field_qual: 'idx'
func_qual: 'pub'

func_stmt: pass_stmt NEWLINE

pass_stmt: 'pass'

########################### const_expr ###############################

const_expr: const_term ('+' const_term | '-' const_term)*
const_term: const_factor ('*' const_factor | '/' const_factor | '%' const_factor)*
const_factor: ('+' | '-' | '~') const_factor | const_power
const_power: const_atom '**' const_factor | const_atom

const_atom: const_group | NAME | NUMBER

const_group: '(' const_expr ')'

########################### common ###################################

dotted_name: NAME ('.' NAME)*
